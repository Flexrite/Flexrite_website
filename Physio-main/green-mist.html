<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Green Mist</title>
    <style>
        @import url('https://fonts.googleapis.com/css?family=Raleway:200');

        body {
          margin: 0px;
          overflow: hidden;
        }

        shader-doodle {
          width: 100vw;
          height: 100vh;
          
          position: absolute;
          z-index: -1;
          top: 0px;
        }

        #container {
          width: 100vw;
          height: 100vh;
          
          display: inline-block;
        }

        h1 {
          margin: 0px;
          width: 100vw;
          line-height: 100vh;
          
          text-align: center;
          
          font-family: "Raleway";
          font-weight: 200;
          font-size: 100pt;
          
          color: #ddffdd;
          text-shadow: 0px 0px 40px #ddffdd;
          user-select: none;
          
          filter: blur(0.5px);
        }
    </style>
    <script src="https://unpkg.com/shader-doodle"></script>
</head>
<body>
    <!-- Shader uniforms (variables):
    uniform float u_time: shader playback time (in seconds)
    uniform float u_delta: delta time between frames (in seconds)
    uniform int u_frame: shader playback frame
    uniform vec4 u_date: year, month, day and seconds
    uniform vec2 u_resolution: viewport resolution (in pixels)
    uniform vec2 u_mouse: mouse pixel coords (x & y)

    Set the gl_FragColor to set the pixel color.
    -->

    <shader-doodle>
      // https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
      vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }
      
      // https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
      float snoise(vec2 v) {
        const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                 -0.577350269189626, 0.024390243902439);
        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);
        vec2 i1;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;
        i = mod(i, 289.0);
        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
        + i.x + vec3(0.0, i1.x, 1.0 ));
        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
          dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;
        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;
        m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
      }
      
      // https://github.com/hughsk/glsl-hsv2rgb
      vec3 hsv2rgb(vec3 c) {
        vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
        vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
        return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
      }
      
      vec3 hsv2rgb(float h, float s, float v) {
        return hsv2rgb(vec3(h / 360., s / 100., v / 100.));
      }
      
      float lerp(float a, float b, float w) {
        return a * (1. - w) + b * w;
      }
      
      float constrain(float v, float minValue, float maxValue) {
        return min(max(v, minValue), maxValue);
      }
      
      void main() {
        vec2 distortion = vec2(snoise(gl_FragCoord.xy / 150.0 + vec2(u_time * .3, u_time * .4)), snoise(gl_FragCoord.xy / 150.0 + vec2(3. + u_time * .4, 4. + u_time * .3)));
        vec2 distorted = gl_FragCoord.xy / 600. + distortion * 0.2;
        vec2 distortion_2 = vec2(snoise(distorted + vec2(u_time * -.01, u_time * -.02)), snoise(distorted + vec2(3. + u_time * -.02, 4. + u_time * -.3)));
        vec2 distorted_2 = gl_FragCoord.xy / 600. + distortion_2 * 0.2;
        float noiseValue = (snoise(distorted_2 + vec2(u_time * -0.05, u_time * 0.05)) + 1.) / 2.;
        
        const float h0_min = 200.;
        const float s0_min = 70.;
        const float v0_min = 10.;
        
        const float h0_max = 160.;
        const float s0_max = 50.;
        const float v0_max = 15.;
        
        const float h1_min = 160.;
        const float s1_min = 40.;
        const float v1_min = 25.;
        
        const float h1_max = 120.;
        const float s1_max = 20.;
        const float v1_max = 35.;
      
        float d = constrain(1. - distance(gl_FragCoord.xy + ((distorted - vec2(1., 1.)) / 2.) * 1000., u_resolution / 2.) / sqrt(u_resolution.x * u_resolution.x * 0.5 + u_resolution.y * u_resolution.y * 0.5), 0., 1.);
        
        d = pow(d, 1.2);
        
        float h_min = lerp(h0_min, h1_min, d);
        float s_min = lerp(s0_min, s1_min, d);
        float v_min = lerp(v0_min, v1_min, d);
        
        float h_max = lerp(h0_max, h1_max, d);
        float s_max = lerp(s0_max, s1_max, d);
        float v_max = lerp(v0_max, v1_max, d);
        
        vec3 color = hsv2rgb(lerp(h_min, h_max, noiseValue), lerp(s_min, s_max, noiseValue), lerp(v_min, v_max, noiseValue));
        
        gl_FragColor = vec4(color.r, color.g, color.b, 1.);
      }
    </shader-doodle>

    <div id="container">
      <span class="vertical-middle"><h1>Green Mist</h1></span>
    </div>
</body>
</html>